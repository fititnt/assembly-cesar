
	ORG	0

	MOV	#Stack,R6


	MOV	#1200,-(R6)		; n
	MOV	#String,-(R6)	; s
	JSR	R7,uint2str
	ADD	#4,R6


	HLT


;**********************************************************
; void uint2str(char *s, uint n)
;	2(R6) -> n -> R2
;	4(R6) -> s -> R1
;
;	Lacos  -> R5
;	pTab10 -> R4
;	dig    -> R3
;
;**********************************************************
Tab10:		DAW	10000, 1000, 100, 10
uint2str:
	MOV	2(R6),R1	; R1 = s
	MOV	4(R6),R2	; R2 = n

	MOV	#Tab10,R4	; pTab10 = Tab10;
	MOV	#4,R5		; Lacos = 4;

Loop:				; do {
	MOV	#'0',R3		; 	dig = '0';

LoopInterno:
	CMP	R2,(R4)		; 	while (n >= *pTab10) {
	BLT	EhMenor
	INC	R3			; 		++dig;
	SUB	(R4),R2		; 		n -= *pTab10;
	JMP	LoopInterno	; 	}

EhMenor:
	MOV	-1(R1),R0	; 	*s++ = dig;
	AND	#HFF00,R0
	OR	R3,R0
	MOV	R0,-1(R1)
	INC	R1

	ADD	#2,R4		; 	++pTab10;

	SOB	R5,Loop		; } while (--Lacos);

	ADD	#'0',R2		; *s++ = n + ‘0’;
	MOV	-1(R1),R0
	AND	#HFF00,R0
	OR	R2,R0
	MOV	R0,-1(R1)
	INC	R1

	MOV	-1(R1),R0	; *s++ = '\0;
	AND	#HFF00,R0
	MOV	R0,-1(R1)

	RTS	R7




; Tamanho do stack de 1024 palavras
			DAW	[1024]
Stack:

	org	65500
String:		DAB	'     '


